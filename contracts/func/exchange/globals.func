;; global variables

{-
meta_data#00 data:(HashMapE 256 ^Cell) : MetaData

storage#_ admin_address:MsgAddress jetton_wallet:MsgAddress
          jetton_reserve:(VarUInteger 16) lp_supply:(VarUInteger 16)
          lp_wallet_code:^Cell, lp_meta_data:^MetaData = Storage;
-}

global int ton_reserve;

global slice admin_address;
global slice jetton_wallet;
global int jetton_reserve;
global int lp_supply;
global cell lp_wallet_code;
global cell lp_meta_data;


() initialize_globals(int balance, int msg_value) impure inline {
    ton_reserve = balance - msg_value - cfg::for_storage_fee();
    slice ds = get_data().begin_parse();
    admin_address = ds~load_msg_addr();
    jetton_wallet = ds~load_msg_addr();
    jetton_reserve = ds~load_coins();
    lp_supply = ds~load_coins();
    lp_wallet_code = ds~load_ref();
    lp_meta_data = ds.preload_ref();
}


() save_data() impure {
  set_data(begin_cell()
    .store_slice(admin_address)
    .store_slice(jetton_wallet)
    .store_coins(jetton_reserve)
    .store_coins(lp_supply)
    .store_ref(lp_wallet_code)
    .store_ref(lp_meta_data)
    .end_cell());
}